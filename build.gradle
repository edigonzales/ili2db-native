plugins {
    id 'application'
    id 'org.graalvm.buildtools.native' version '0.9.22'
    id 'org.jreleaser' version '1.2.0'
}

def APP_VERSION = "4.11.0"

def myArtifactId = "ili2pg"
def buildNumber = System.env.GITHUB_RUN_NUMBER
def os = org.gradle.nativeplatform.platform.internal.DefaultNativePlatform.currentOperatingSystem; 

dependencies {
    implementation "ch.interlis:ili2pg:${APP_VERSION}" 

    /* 
    Explizite Angabe dieser Dependency ist notwendig, das sonst runtime error.
    Es wird bewusst die non-jre6-Version des JDBC-Treibers verwendet, damit 
    die Probleme mit SCRAM authentification nicht auftreten:
    https://github.com/claeis/ili2db/issues/448
    */
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.2.18' 
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://jars.interlis.ch"
    }
}

graalvmNative {
    toolchainDetection = false
    binaries {
        main {
            imageName = 'ili2pg'
            mainClass = 'ch.ehi.ili2pg.PgMain' 
            debug = false 
            verbose = true 
            fallback = false 
            //configurationFileDirectories.from(file("conf-dir-"+getOperatingSystemName()+"/")) // TODO: mit gui wieder n√∂tig
            configurationFileDirectories.from(file("conf-dir/"))

            buildArgs.add('--enable-url-protocols=http,https') 
            //buildArgs.add('--pgo-instrument') 
            //buildArgs.add("--pgo=$projectDir/conf-dir/default.iprof.gzip") 
            //buildArgs.add('--gc=G1') 

            jvmArgs.add("-Djava.awt.headless=false")

            useFatJar = true
        }
    }
}

distributions {
    nativeImage {
        contents {
            from 'build/native/nativeCompile/'
        }
    }
}

nativeImageDistZip {
    archiveName myArtifactId + '-' + APP_VERSION + "_" + buildNumber + '-' + getOperatingSystemName() + '-' + getArchitectureName() + ".zip"
}

jreleaser {
    gitRootSearch = true
    
    project {
        name = myArtifactId
        description = 'Native binaries for ili2pg.'
        website = 'http://blog.sogeo.services'
        authors = ['edigonzales']
        license = 'MIT'
        docsUrl = 'https://github.com/edigonzales/ili2pg-native'
        copyright = '2022 Stefan Ziegler'
        version = APP_VERSION + "_" + buildNumber
        versionPattern = 'CUSTOM'
        java {
            groupId = 'ch.interlis'
        }
    }

    release {
        github {
            repoOwner = 'edigonzales'
            overwrite = true
            apiEndpoint = 'https://api.github.com'
        }
    }

    distributions {
        appNative {
            distributionType = 'NATIVE_IMAGE'
            stereotype = 'CLI'
            artifact {
                path = "./artifacts-native/ili2pg-${APP_VERSION}_${buildNumber}-linux-x86-64.zip"
                platform = 'linux'
            }
            artifact {
                path = "./artifacts-native/ili2pg-${APP_VERSION}_${buildNumber}-osx-x86-64.zip"
                platform = 'osx'
            }
            artifact {
                path = "./artifacts-native/ili2pg-${APP_VERSION}_${buildNumber}-windows-x86-64.zip"
                platform = 'windows'
            }
        }
    }
}

def getOperatingSystemName() {
    def os = org.gradle.nativeplatform.platform.internal.DefaultNativePlatform.currentOperatingSystem; 
    def osName = os.getName().toLowerCase();
    if (osName.contains("windows")) {
        return "windows";
    } else if (osName.contains("mac os x") || osName.contains("darwin") || osName.contains("osx")) {
        return "osx";
    } else if (osName.contains("linux")) {
        return "linux";
    } else {
        // not strictly true
        return "unix";
    }
}

def getArchitectureName() {
    def arch = org.gradle.nativeplatform.platform.internal.DefaultNativePlatform.currentArchitecture; 
    def archName = arch.getName().toLowerCase();
    return archName;    
}

